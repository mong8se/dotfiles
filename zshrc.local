bindkey -v
if [[ "$TERM" != emacs ]] ; then
    [[ -z "$terminfo[cuu1]"  ]] || bindkey -M viins "$terminfo[cuu1]"  up-line-or-history
    [[ -z "$terminfo[kcuu1]" ]] || bindkey -M viins "$terminfo[kcuu1]" up-line-or-history
    [[ -z "$terminfo[kcud1]" ]] || bindkey -M viins "$terminfo[kcud1]" down-line-or-history
    # ncurses stuff:
    [[ "$terminfo[kcuu1]" == $'\eO'* ]] && bindkey -M viins "${terminfo[kcuu1]/O/[}" up-line-or-history
    [[ "$terminfo[kcud1]" == $'\eO'* ]] && bindkey -M viins "${terminfo[kcud1]/O/[}" down-line-or-history
fi

function zle-line-init zle-keymap-select {
    VIM_MODE_PROMPT="${${KEYMAP/vicmd/C}/(main|viins)/I}"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

function vim_mode_prompt {
    if [[ "$VIM_MODE_PROMPT" == 'C' ]]
    then echo '%S%#%s'
    else echo '%#'
    fi
    VIM_MODE_PROMPT='I'
}

setopt prompt_subst

PS1='%(1j.[$fg[green]%j%{$reset_color%}].)%B[%b%(!.%{$fg[red]%}.%{$fg[blue]%})%m%{$reset_color%}%B]%b$(vim_mode_prompt) '
DONTSETRPROMPT=1
RPS1='${vcs_info_msg_0_}%S %3~ %s'

# Put the penultimate and current directory in the iterm tab:
function settab { print -Pn "\e]1;%m:%2~\a%" }

# Put the string "[zsh] hostname:/full/directory/path" in the title bar:
function settitle { print -Pn "\e]2;%n@%m:%d [zsh]\a" }

REPORTTIME=5
TIMEFMT=$'\e]9;%J\007 \e[7m %J: %P %Mk \e[0m %U user / %S sys / %E total'

# This updates after each change of directory:
function precmd {
    vcs_info
    settab
    settitle
}

NOPRECMD=0
NOTITLE=1

unsetopt AUTO_CD # cd if no matching command
unsetopt beep

alias cd..="cd .."

xsource "${HOME}/.zshrc.local.local"
if [[ `uname` == Darwin ]] then
    xsource "${HOME}/.zshrc.mac"
fi

xsource "${HOME}/.dotfiles/Resources/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

# fasd
eval "$(fasd --init auto)"
alias v='f -e vim' # quick opening files with vim
